plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.5.0"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge" version "8f14deb"
}

ext.kotlin_version = "1.5.0"

version = "1.0.6"
group = "mynameisjeff"
archivesBaseName = "SkyblockClient-Updater"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"
compileKotlin.kotlinOptions.jvmTarget = "1.8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    gitVersion = false
}

configurations {
    // Creates an extra configuration that implements `implementation` to be used later as the configuration that shades libraries
    include
    implementation.extendsFrom(include)
}

dependencies {
    // How to normally add a dependency (If you don't want it to be added to the jar)
    // implementation "com.example:example:1.0.0"
    // If you would like to include it (have the library inside your jar) instead use
    // include "com.example:example:1.0.0"
    include "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    include "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    include "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_version"
}

/**
 * This task simply replaces the `${version}` and `${mcversion}` properties in the mcmod.info with the data from Gradle
 */
processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/**
 * This task simply moves resources so they can be accessed at runtime, Forge is quite weird isn't it
 */
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
            todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources

jar {
    enabled = false
}

// This adds support to ("embed", "shade", "include") libraries into our JAR
shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    relocate("kotlinx.coroutines", "mynameisjeff.skyblockclientupdater.coroutines")

    exclude "**/module-info.class"
    exclude "META-INF/versions/**"
}

tasks.reobfJar.dependsOn(shadowJar)